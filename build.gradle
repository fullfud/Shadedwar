plugins {
    id "java"
    id "net.minecraftforge.gradle" version "6.0.+"
}

group = "com.fullfud"

base {
    archivesName = "shadedwar"
}

def buildNumberFile = file("build_number.properties")
def readBuildNumber = {
    if (!buildNumberFile.exists()) {
        return 0
    }
    def text = buildNumberFile.getText("UTF-8")
    def m = (text =~ /(?m)^buildNumber\s*=\s*(\d+)\s*$/)
    if (m.find()) {
        return Integer.parseInt(m.group(1))
    }
    return 0
}
def writeBuildNumber = { int n ->
    buildNumberFile.write("buildNumber=${n}\n", "UTF-8")
}

def baseVersion = (findProperty("mod_base_version") ?: "2.0.0").toString()
def channel = (findProperty("mod_channel") ?: "BETA").toString()
def buildNumber = readBuildNumber()
version = "${baseVersion}-${channel}.${buildNumber}"

def bumpTaskNames = ["build", "assemble", "jar", "reobfJar"] as Set
def debugVersion = (findProperty("debugVersion") ?: "false").toString().toBoolean()
gradle.taskGraph.whenReady { graph ->
    if (debugVersion) {
        println("[FULLFUD] taskGraph=" + graph.allTasks.collect { it.path }.join(", "))
        println("[FULLFUD] buildNumberFile=" + buildNumberFile.absolutePath + " exists=" + buildNumberFile.exists())
        if (buildNumberFile.exists()) {
            println("[FULLFUD] buildNumberFileContents=" + buildNumberFile.getText("UTF-8").replace("\r", "\\r").replace("\n", "\\n"))
        }
    }
    if (!graph.allTasks.any { bumpTaskNames.contains(it.name) }) {
        if (debugVersion) {
            println("[FULLFUD] build number not bumped (no bump tasks present)")
        }
        return
    }

    buildNumber = readBuildNumber() + 1
    writeBuildNumber(buildNumber)
    project.version = "${baseVersion}-${channel}.${buildNumber}"
    if (debugVersion) {
        println("[FULLFUD] bumped buildNumber=" + buildNumber + " version=" + project.version)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

minecraft {
    mappings channel: "official", version: "1.20.1"

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "info"
            mods {
                fullfud {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "info"
            mods {
                fullfud {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "info"
            args "--mod", "fullfud", "--all", "--output", file("src/generated/resources").absolutePath, "--existing", file("src/main/resources").absolutePath
            mods {
                fullfud {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "GeckoLib"
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
    }
}

def latticeVersion = "3.0.0"
def latticeDevJar = files("lattice/build/libs/lattice-${latticeVersion}.jar")
def latticeReobfJar = files("lattice/build/reobfJar/output.jar")

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.1.0"
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-1.20.1:4.3.1")
    compileOnly latticeDevJar
    runtimeOnly latticeDevJar
}

sourceSets {
    main {
        resources.srcDir "src/generated/resources"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 17
}

tasks.named("compileJava") {
    dependsOn(":lattice:jar")
}

jar {
    archiveVersion.set(providers.provider { project.version.toString() })
    dependsOn(":lattice:reobfJar")
    manifest {
        attributes([
            "Specification-Title": "shadedwar",
            "Specification-Vendor": "fullfud",
            "Specification-Version": "${-> project.version}",
            "Implementation-Title": project.name,
            "Implementation-Version": "${-> project.version}",
            "Implementation-Vendor": "fullfud"
        ])
    }
    from(latticeReobfJar) {
        into "META-INF/jarjar"
        rename { "lattice-${latticeVersion}.jar" }
    }
}

jar.finalizedBy("reobfJar")

def cleanResourcesDir = file("src/clean/resources")
def cleanResourcesOut = layout.buildDirectory.dir("cleanResources")

tasks.register("syncCleanResources", Copy) {
    dependsOn("processResources")
    from(tasks.named("processResources").map { it.destinationDir }) {
        if (cleanResourcesDir.exists()) {
            exclude { details ->
                file("${cleanResourcesDir}/${details.path}").exists()
            }
        }
    }
    if (cleanResourcesDir.exists()) {
        from(cleanResourcesDir)
    }
    into(cleanResourcesOut)
}

tasks.register("jarClean", Jar) {
    archiveVersion.set(providers.provider { project.version.toString() })
    archiveClassifier.set("clean")
    dependsOn("classes", "syncCleanResources", ":lattice:reobfJar")
    manifest {
        attributes([
            "Specification-Title": "shadedwar",
            "Specification-Vendor": "fullfud",
            "Specification-Version": "${-> project.version}",
            "Implementation-Title": project.name,
            "Implementation-Version": "${-> project.version}",
            "Implementation-Vendor": "fullfud"
        ])
    }
    from(sourceSets.main.output.classesDirs)
    from(cleanResourcesOut)
    from(latticeReobfJar) {
        into "META-INF/jarjar"
        rename { "lattice-${latticeVersion}.jar" }
    }
}

reobf {
    jarClean {}
}

tasks.named("jarClean") {
    finalizedBy("reobfJarClean")
}

tasks.register("buildClean") {
    dependsOn("reobfJarClean")
}

processResources {
    inputs.property "version", providers.provider { project.version.toString() }
    filesMatching("META-INF/mods.toml") {
        expand(["version": "${-> project.version}"])
    }
}

tasks.named("build") {
    dependsOn("reobfJar")
}
